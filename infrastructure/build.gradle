plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id("com.vanniktech.maven.publish") version "0.34.0"
}

mavenPublishing {
    def resolvedGroup = 'io.github.digitalhorizonsco'
    def resolvedArtifact = 'infrastructure'
    def resolvedVersion = '0.0.2'

    coordinates(resolvedGroup.toString(), resolvedArtifact.toString(), resolvedVersion.toString())

    pom {
        name.set(project.findProperty('POM_NAME') ?: project.name)
        description.set(project.findProperty('POM_DESCRIPTION') ?: "A library from the Polaris Design System")
        inceptionYear.set(project.findProperty('POM_INCEPTION_YEAR') ?: '')
        url.set(project.findProperty('POM_URL') ?: 'https://github.com/digitalHorizonsCo/Polaris-Design-System')

        licenses {
            license {
                name.set(project.findProperty('POM_LICENSE_NAME') ?: 'The Apache License, Version 2.0')
                url.set(project.findProperty('POM_LICENSE_URL') ?: 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                distribution.set(project.findProperty('POM_LICENSE_DISTRIBUTION') ?: 'repo')
            }
        }

        developers {
            developer {
                id.set(project.findProperty('POM_DEVELOPER_ID') ?: project.findProperty('developerId') ?: 'unknown')
                name.set(project.findProperty('POM_DEVELOPER_NAME') ?: project.findProperty('developerName') ?: 'Unknown')
                email.set(project.findProperty('POM_DEVELOPER_EMAIL') ?: project.findProperty('developerEmail') ?: '')
            }
        }

        scm {
            url.set(project.findProperty('POM_SCM_URL') ?: '')
            connection.set(project.findProperty('POM_SCM_CONNECTION') ?: '')
            developerConnection.set(project.findProperty('POM_SCM_DEV_CONNECTION') ?: '')
        }
    }
    publishToMavenCentral()

    signAllPublications()
}

android {
    namespace 'com.digitalhorizons.infrastructure'
    compileSdk 36

    defaultConfig {
        minSdk 21

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildFeatures {
        buildConfig = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation libs.androidxCoreKtx
    implementation libs.androidxAppcompat
    testImplementation libs.junit
    api libs.kotlinxCoroutinesCore
    api libs.kotlinxCoroutinesAndroid
    api libs.moshi
    api libs.converterMoshi
    api libs.kotlinxSerialization
    api libs.converterKotlinxSerialization
    implementation libs.timber
    api libs.retrofit
    api libs.loggingInterceptor
}

